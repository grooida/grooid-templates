ext {
    versions = [
        grooid_new_library: new File('templates/grooid-new-library/VERSION').text.trim(),
        grooid_new_project: new File('templates/grooid-new-project/VERSION').text.trim()
    ]
}

buildscript {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/pledbrook/plugins" }
    }
    dependencies {
        classpath "uk.co.cacoethes:lazybones-gradle:1.2.3"
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: "uk.co.cacoethes.lazybones-templates"
apply plugin: 'org.asciidoctor.convert'

lazybones {
    licenses = ["Apache-2.0"]
    repositoryName = "marioggar/grooid-templates"
    repositoryUsername = project.properties.bintrayUser
    repositoryApiKey = project.properties.bintrayKey
}

asciidoctor {
    sourceDir 'docs'
    attributes 'source-highlighter'  :  'prettify',
               toc                   :  'left',
               icons                 :  'font',
               toclevels             :  4,
               stylesheet            : 'css/style.css',
               'pdf-style'           :  'default',
               'grooid-new-project'  :  versions.grooid_new_project,
               'grooid-new-library'  :  versions.grooid_new_library
    resources {
        from(sourceDir) {
            include 'css/**'
            include 'imgs/**'
        }
    }
}
